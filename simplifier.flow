import ar;


export {
    simplifyArExpr(expr : ArExpr) -> ArExpr;

    differentiateExpr(expr : ArExpr, var : string) -> ArExpr;
}

differentiateExpr(expr : ArExpr, var : string) -> ArExpr {
    switch(expr){
        ArDer(e, v) : differentiateExpr(differentiateExpr(e, v), var);
        ArDeg(v, d) : ArMult(ArMult(d, v), differentiateExpr(v, var));
        ArSum(l, r) : ArSum(differentiateExpr(l, var), differentiateExpr(r, var));
        ArDiff(l, r) : ArDiff(differentiateExpr(l, var), differentiateExpr(r, var));
        ArMult(l, r) : ArSum(ArMult(differentiateExpr(l, var), r), ArMult(l, differentiateExpr(r, var)));
        ArDiv(l, r) : ArDiv(ArDiff(ArMult(differentiateExpr(l, var), r), ArMult(l ,differentiateExpr(r, var))), ArMult(r, r));
        ArNeg(v) : ArNeg(differentiateExpr(v, var));
        ArInt(v) : ArInt(0);
        ArVar(v) : if(v == var) ArInt(1) else ArInt(0)
    }
}

simplifyArExpr(expr : ArExpr) -> ArExpr {
    switch(expr) {
        ArDer(e, v) : simplifyArExpr(differentiateExpr(e, v));

        ArDeg(v, d) : {
            if (d == ArInt(0)) ArInt(0)
            else if (d == ArInt(1)) simplifyArExpr(v)
            else ArDeg(simplifyArExpr(v), d)
        }

        ArSum(l, r) : {
            l1 = simplifyArExpr(l);
            r1 = simplifyArExpr(r);

            if(r1 == ArInt(0)) l1
            else if(l1 == ArInt(0)) r1
            else ArSum(l1, r1)
        }

        ArDiff(l, r) : {
            l1 = simplifyArExpr(l);
            r1 = simplifyArExpr(r);

            if(r1 == ArInt(0)) l1
            else if(l1 == ArInt(0)) simplifyArExpr(ArNeg(r1))
            else ArDiff(l1, r1)
        }

        ArMult(l, r) : {
            l1 = simplifyArExpr(l);
            r1 = simplifyArExpr(r);

            if(r1 == ArInt(0) || l1 == ArInt(0)) ArInt(0)
            else if(r1 == ArInt(1)) l1
            else if(l1 == ArInt(1)) r1
            else if(r1 == ArInt(-1)) simplifyArExpr(ArNeg(l1))
            else if(l1 == ArInt(-1)) simplifyArExpr(ArNeg(r1))
            else ArMult(l1, r1)
        }

        ArDiv(l, r) : {
            l1 = simplifyArExpr(l);
            r1 = simplifyArExpr(r);

            if(r1 == ArInt(1)) l1
            else if(r1 == ArInt(-1)) simplifyArExpr(ArNeg(l1))
            else if(l1 == ArInt(0) && r1 != ArInt(0)) ArInt(0)
            else ArDiv(l1, r1)
        }

        ArNeg(v) : {
            v1 = simplifyArExpr(v);

            switch(v1){
                ArNeg(v2) : v2;
                ArInt(v2) : ArInt(-v2);
                default : ArNeg(v1);
            }
        }

        ArVar(v) : ArVar(v);

        ArInt(v) : ArInt(v);
    }
}